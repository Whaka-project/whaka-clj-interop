apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.vsubhuman'
archivesBaseName = "clj-interop"
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
	
	def clojure = 'org.clojure:clojure:1.8.0'
    compileOnly clojure
	testCompile clojure
	
	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

if (hasProperty('signing.keyId')) {
	signing {
		sign configurations.archives
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.6'
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment {
					MavenDeployment deployment -> signing.signPom(deployment)
				}
	
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
	
				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
	
				pom.project {
					name archivesBaseName
					packaging 'jar'
					// optionally artifactId can be defined here
					description 'Clojure interoperability tool'
					url 'https://github.com/vantuz-subhuman/clj-interop'
	  
					scm {
						connection 'scm:git:https://vantuz-subhuman@github.com/vantuz-subhuman/clj-interop.git'
						developerConnection 'scm:git:https://vantuz-subhuman@github.com/vantuz-subhuman/clj-interop.git'
						url 'https://github.com/vantuz-subhuman/clj-interop'
					}
	  
					licenses {
						license {
							name 'MIT License'
							url 'http://www.opensource.org/licenses/mit-license.php'
							distribution 'repo'
						}
					}
					
					developers {
						developer {
							id 'vantuz-subhuman'
							name 'Vantuz Subhuman'
							email 'vantuz-subhuman@yandex.ru'
							url 'https://github.com/vantuz-subhuman'
						}
					}
				}
			}
		}
	}
}